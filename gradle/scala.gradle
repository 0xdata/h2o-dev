apply plugin: 'scala'

//
// The Scala module expects scalaVersion and scalaBaseVersion
// to be set up as extension properties on this project.
//

// Setup additional sources specific to version
sourceSets {
    main {
        scala {
            srcDir "src/main/scala_$scalaBaseVersion"
        }
    }
    test {
        scala {
            srcDirs "src/test/scala_$scalaBaseVersion"
        }
    }
}

// Activate Zinc compiler and configure scalac
tasks.withType(ScalaCompile) {
	configure(scalaCompileOptions.forkOptions) {
        jvmArgs = ['-XX:MaxPermSize=256m']
    }
}

// Create jar
task testJar(type: Jar, dependsOn: testClasses) {
    appendix = 'test'
    classifier = 'tests'
    from sourceSets.test.output
}

// Create jar with scaladoc
task scaladocJar(type: Jar) {
    classifier = 'scaladoc'
    from scaladoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Create a configuration containing only for test artifacts
configurations {
    testArchives
    toPublish
}

// Explicitly
artifacts {
    testArchives testJar
    toPublish scaladocJar
    toPublish testJar
    toPublish sourcesJar
}

// Enable support for multi-node testing
apply from: "$rootDir/gradle/multiNodeTesting.gradle"

