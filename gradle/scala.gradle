import org.gradle.api.plugins.internal.JvmPluginsHelper

apply plugin: 'scala'

//
// The Scala module expects scalaVersion and scalaBaseVersion
// to be set up as extension properties on this project.
//

// Setup additional sources specific to version
sourceSets {
    main {
        scala {
            srcDir "src/main/scala_$scalaBaseVersion"
        }
    }
    test {
        scala {
            srcDirs "src/test/scala_$scalaBaseVersion"
        }
    }
}

task scaladocJar(type: Jar) {
    classifier = 'scaladoc'
    from scaladoc
}

JvmPluginsHelper.configureDocumentationVariantWithArtifact(
        "javadocElements", (String) null, "javadoc", [], "scaladocJar", tasks.named("scaladocJar"),
        JvmPluginsHelper.findJavaComponent(this.components),
        configurations, tasks, project.getObjects()
)


// Activate Zinc compiler and configure scalac
tasks.withType(ScalaCompile) {
	configure(scalaCompileOptions.forkOptions) {
        jvmArgs = ['-XX:MaxPermSize=256m']
    }
}

// Create jar
task testJar(type: Jar, dependsOn: testClasses) {
    appendix = 'test'
    from sourceSets.test.output
}

// Enable support for multi-node testing
apply from: "$rootDir/gradle/multiNodeTesting.gradle"

