//
// Micro benchmark support
//

apply plugin: 'me.champeau.gradle.jmh'

def resultsFilename() {
    H2OBuildVersion bv = new H2OBuildVersion(rootDir, version);
    String date = new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("UTC"))
    return "${bv.getLastCommitHash()}-${date}.csv"
}

ext {
    jmhResultFileName = "${resultsFilename()}"
}

// Setup JMH
jmh {
    //
    // Include only selected tests for benchmarking
    // include = 'water.KeyEqualsBench'
    jmhVersion = '1.15'
    resultsFile = project.file("${project.buildDir}/reports/jmh/$jmhResultFileName")
    duplicateClassesStrategy = 'warn'
    resultFormat = 'SCSV'
    //
    // Attach different profilers (gc, stack summary, ...)
    // See: http://java-performance.info/introduction-jmh-profilers/
    //profilers = ['stack']
    //profilers = ['hs_comp']
}

dependencies {
    jmh group: 'commons-io' , name: 'commons-io', version: '2.4'
}

task uploadResultsToS3(type: water.build.tasks.S3UploadTask) {
    bucket = "ai.h2o.ubench"
    objectName = "${project.name}/$jmhResultFileName"
    file = jmh.resultsFile
}

task ubench
ubench.dependsOn "jmh"
ubench.dependsOn uploadResultsToS3
uploadResultsToS3.shouldRunAfter("jmh")
